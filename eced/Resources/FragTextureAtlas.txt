#version 330 core

smooth in vec2 tileloc;

out vec4 color;

uniform sampler2D atlas;
uniform sampler2D numbers;
uniform isampler2D texInfo;
uniform isampler2D mapPlane;
uniform float tilesize;

void main()
{
	//gross hack
	ivec2 itile = ivec2(tileloc);

	ivec4 tilemapInfo = texelFetch(mapPlane, itile, 0);

	//The type of the tile. 1 is a normal tile, 2 is a hole exposing either a floor, ceiling, or floorcode.
	int type = tilemapInfo.r & 255;

	//The mode to draw the tile in.  0 is a floor code, 1 is a textured tile, 2 is a solid color.
	int mode = (tilemapInfo.r >> 8) & 255;

	int tileid = tilemapInfo.g;
	int floorid = tilemapInfo.g;

	float multiplier = 0.5;

	int neighbors = 0;
	//Check if neighbors are empty (id 2) instead of tile (id 1)
	if (type == 1)
	{
		if ((texelFetch(mapPlane, ivec2(itile.x - 1, itile.y), 0).r & 255) == 2)
		{
			neighbors |= 1;
		}
		if ((texelFetch(mapPlane, ivec2(itile.x + 1, itile.y), 0).r & 255) == 2)
		{
			neighbors |= 2;
		}
		if ((texelFetch(mapPlane, ivec2(itile.x, itile.y - 1), 0).r & 255) == 2)
		{
			neighbors |= 4;
		}
		if ((texelFetch(mapPlane, ivec2(itile.x, itile.y + 1), 0).r & 255) == 2)
		{
			neighbors |= 8;
		}
	}
	else
	{
		multiplier = tilemapInfo.a / 255.0;
	}

	ivec4 textureInfo;

	ivec2 cellpos;
	vec2 cellposf;

	if (mode == 0) //Show floor code
	{
		ivec2 digit1;
		ivec2 digit2;

		cellposf = vec2((tileloc.x - itile.x) * 2 , (tileloc.y - itile.y) * 2);
		if (cellposf.y >= 1)
		{
			digit1 = ivec2((floorid >> 8) / 16, 0) * 8;
			digit2 = ivec2((floorid >> 8) % 16, 0) * 8;
			if (cellposf.x >= 1)
				cellpos = ivec2(digit2.x + int((cellposf.x - 1) * 8), digit2.y + int((cellposf.y - 1) * 8));
			else
				cellpos = ivec2(digit1.x + int(cellposf.x * 8), digit1.y + int((cellposf.y - 1) * 8));
		}
		else
		{
			digit1 = ivec2((floorid & 255) / 16, 0) * 8;
			digit2 = ivec2(floorid % 16, 0) * 8;
			if (cellposf.x >= 1)
				cellpos = ivec2(digit2.x + int((cellposf.x - 1) * 8), digit2.y + int(cellposf.y * 8));
			else
				cellpos = ivec2(digit1.x + int(cellposf.x * 8), digit1.y + int(cellposf.y * 8));
		}
	}
	else if (mode == 1) //Show textured wall
	{
		textureInfo = texelFetch(texInfo, ivec2(0, tileid), 0);

		//Texture information
		ivec2 textureRes = textureInfo.rg;
		ivec2 texOrigin = textureInfo.ba;

		//Texture UV in this tile.
		vec2 cellpost;

		//Scales the texture to normalized units
		vec2 texScale = vec2(tilesize / textureInfo.r, tilesize / textureInfo.g);

		//Find fractional position in the cell
		cellposf = vec2(tileloc.x - itile.x , tileloc.y - itile.y);

		//Find the UV coordinates. This needs to be projected differently for flats.
		if (type == 2)
		{
			cellpost = fract(tileloc * texScale);
		}
		else
		{
			cellpost = fract(cellposf * texScale);
		}
		cellpos = ivec2(texOrigin.x + int(cellpost.x * textureRes.x), texOrigin.y + int(cellpost.y * textureRes.y));
	}

	//Darken the edges of tiles with neighboring empty cells
	if (neighbors != 0)
	{
		multiplier = 1.0;
		if ((neighbors & 1) != 0)
		{
			multiplier *= (clamp(cellposf.x * 5.0, 0.0, 1.0));
		}
		if ((neighbors & 2) != 0)
		{
			multiplier *= (clamp((1.0 - cellposf.x) * 5.0, 0.0, 1.0));
		}
		if ((neighbors & 4) != 0)
		{
			multiplier *= (clamp(cellposf.y * 5.0, 0.0, 1.0));
		}
		if ((neighbors & 8) != 0)
		{
			multiplier *= (clamp((1.0 - cellposf.y) * 5.0, 0.0, 1.0));
		}
	}

	if (mode == 0) //Show floor code
	{
		color = texelFetch(numbers, cellpos, 0);
	}
	else if (mode == 1) //Normal tile
	{
		color = texelFetch(atlas, cellpos, 0) * multiplier;
		//hack
		color.a = 1.0;
	}
	else if (mode == 2) //Solid color tile
	{
		int r = tilemapInfo.g & 255;
		int g = (tilemapInfo.g >> 8) & 255;
		color = vec4(r / 255.0, g / 255.0, tilemapInfo.b / 255.0, 1.0f) * multiplier;
		//hack
		color.a = 1.0;
	}
}